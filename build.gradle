plugins {
    id 'java'
    id 'application'
    id 'com.gradleup.shadow' version '9.2.2'
}

group = 'joseta'
version = '2.0.0'

sourceSets.main.java.srcDirs = ['src/main/java']
sourceSets.main.resources.srcDirs = ['src/main/resources']

def generatedSourcesDir = layout.buildDirectory.dir("generated/sources/annotationProcessor/java/main")
sourceSets.main.java.srcDir generatedSourcesDir

repositories { mavenCentral() }

dependencies {
    implementation('net.dv8tion:JDA:6.1.3') {
      // Optionally disable audio natives to reduce jar size by excluding `opus-java` and `tink`
      exclude module: 'opus-java'
      exclude module: 'tink'
    }
    implementation 'ch.qos.logback:logback-classic:1.5.21'
    implementation 'io.github.cdimascio:dotenv-java:3.2.0'
    implementation 'org.reflections:reflections:0.10.2'

    implementation 'org.postgresql:postgresql:42.7.8'
    implementation 'org.hibernate:hibernate-core:7.1.11.Final'
    // Hibernate Processor
    annotationProcessor 'org.hibernate.orm:hibernate-processor:7.1.11.Final'
    // Agroal connection pool
    runtimeOnly 'org.hibernate.orm:hibernate-agroal:7.1.11.Final'
}

apply from: 'gradle/jdaEventEnumCodeGen.gradle'
tasks.named('compileJava') { dependsOn tasks.named('generateJdaEventEnum') }

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.incremental = true

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    options.compilerArgs << '-parameters' // Enable parameter name retention at runtime, required for the InteractionProcessor

    options.getGeneratedSourceOutputDirectory().set(file(generatedSourcesDir))
}

shadowJar {
    mainClass = 'joseta.JosetaBot'
    archiveFileName = 'JosetaBot.jar'
}
